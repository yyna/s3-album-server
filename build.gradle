plugins {
  id "gradle-clojure.clojure" version "0.4.0"
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url "https://clojars.org/repo" }
  maven { url "https://repo1.maven.org/maven2/" }
}

dependencies {
  compile(
      'com.amazonaws:aws-lambda-java-core:1.2.0',
      'com.amazonaws:aws-lambda-java-log4j:1.0.0'
  )

  implementation 'org.clojure:clojure:1.9.0'
  implementation 'com.cognitect.aws:api:0.8.515'
  implementation 'com.cognitect.aws:s3:811.2.934.0'
  implementation 'com.cognitect.aws:endpoints:1.1.12.42'
}

clojure {
  builds {
    main {
      aotAll()
    }
  }
}

task(buildJson) {
  doLast {
    new File("$buildDir/build.json").newWriter().withWriter { w ->
      w << groovy.json.JsonOutput.toJson([distribution: relativePath(shadowJar.archivePath), version: shadowJar.version])
    }
  }
}

shadowJar.finalizedBy(buildJson)
build.dependsOn shadowJar
